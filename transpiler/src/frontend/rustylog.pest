Program = { SOI ~ (Struct|Impl)* ~ EOI }

Struct = { Visibility? ~ "struct" ~ Ident ~ "{" ~ StructVariables ~ "}" }
Impl = { "impl" ~ Ident ~ "{" ~ Function* ~ "}" }

StructVariable = { Visibility? ~ Ident ~ ":" ~ Type }
StructVariables = _{ StructVariable ~ ("," ~ StructVariable)* ~ ","? }

Function = { FnMacro? ~ Visibility? ~ "fn" ~ Ident ~ "(" ~ Args? ~ ")" ~ ("->" ~ Type)? ~ BlockStmt }

FnMacro = { AlwaysFF | AlwaysComb }
AlwaysFF = { "#[always_ff(" ~ AlwaysFFArgs ~ ")]" }
AlwaysComb = { "#[always_comb]" }
AlwaysFFArg = { Ident ~ (":" ~ Trigger)? }
AlwaysFFArgs = _{ AlwaysFFArg ~ ("," ~ AlwaysFFArg)* ~ ","? }

Args = _{ (Arg | SelfRef) ~ ("," ~ Arg)* ~ ","? }
Arg = { Ident ~ ":" ~ Type }
SelfRef = { "&" ~ Mut? ~ "self" }

Type = { IOType ~ "<" ~ SubType ~ ">" | SubType }
SubType = { ArrayType | LogicType | TriType | QName }
ArrayType = { "[" ~ SubType ~ ";" ~ Index ~ "]" }
LogicType = @{ "logic" }
TriType = @{ "tri" }


Statement = { IfStmt
		  	| AssignStmt
			| BlockStmt
}

AtomExpr = _{ Int
		   | SubExpr
		   | ArrayExpr
		   | ConstructExpr
		   | CallExpr
		   | NameExpr
}
ExprList = _{ Expression ~ ("," ~ Expression)* ~ ","? }
Constructor = { Ident ~ (":"  ~ Expression)? }
ConstructorList = _{ Constructor ~ ("," ~ Constructor)* ~ ","? }

NameExpr = { VName }
CallExpr = { VName ~ "(" ~ ExprList? ~ ")" }
ArrayExpr = { "[" ~ ExprList? ~ "]" }
ConstructExpr = { QName ~ "{" ~ ConstructorList? ~ "}" }
SubExpr = { "(" ~ Expression ~ ")" }

IndexExpr = { AtomExpr ~ ("[" ~ Index ~ "]")* }
UnaryExpr = { IndexExpr | UnaryOp ~ UnaryExpr }
MulDivExpr = { UnaryExpr ~ (MulDivOp ~ UnaryExpr)* }
AddSubExpr = { MulDivExpr ~ (AddSubOp ~ MulDivExpr)* }
Expression = { AddSubExpr }

BlockStmt = { "{" ~ Statement* ~ "}" }
IfStmt = { "if" ~ Expression ~ BlockStmt ~ ("else" ~ BlockStmt)? }
AssignStmt = { VName ~ AssignOp ~ Expression ~ ";" }


VName = { ("self" ~ ".")? ~ Ident }
QName = { (Ident ~ "::")* ~ Ident }
Index = { Int ~ (".." ~ Int)? }

Int = @{ ASCII_DIGIT+ }
Ident = @{ ('a'..'z' | 'A'..'Z' | "_") ~ ('a'..'z' | 'A'..'Z' | '0'..'9' | "_")* }
UnaryOp = @{ "+" | "-" | "~" }
MulDivOp = @{ "*" | "/" }
AddSubOp = @{ "+" | "-" }
AssignOp = @{ "=" | "<=" }
Trigger = @{ "posedge" | "negedge" }
IOType = @{ "Input" | "Output" | "InOut" }
Visibility = @{ "pub" }
Mut = @{ "mut" }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }